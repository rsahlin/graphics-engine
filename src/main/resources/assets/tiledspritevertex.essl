/* Copyright 2015 Richard Sahlin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

 /**
 * Fragment shader for tiled sprite renderer
 * @author Richard Sahlin
 */
#version 100

//Put array declaration after name for GLSL compatibility
uniform mat4 uMVPMatrix;
uniform vec3 uSpriteData; //tex width, tex height, frames per line

attribute vec4 aPosition;
attribute vec4 aTileSprite; //sprite x, sprite y, sprite, z, frame 
attribute vec4 aTileSprite2;//texture u, texture v,  rotation z, unused

varying vec2 vTexCoord;
mat4 calculateTransformMatrix();

void main() {
    gl_Position = calculateTransformMatrix() * aPosition + (uMVPMatrix * vec4(aTileSprite.x, aTileSprite.y, aTileSprite.z, 0.0));
    float y = floor(aTileSprite.w / uSpriteData.z);
    vTexCoord = vec2(aTileSprite2.x * uSpriteData.x + 
                     mod(aTileSprite.w, uSpriteData.z) * uSpriteData.x,
                     aTileSprite2.y * uSpriteData.y + y * uSpriteData.y);
}
/**
 * Returns a matrix with z axis rotation set from attribute aTileSprite2.z.
 * @return Matrix with z axis rotation set from aTileSprite2.z
 */
mat4 calculateTransformMatrix(){

    mat4 modelview = mat4(1);

    //Rotate
    float cz = cos(aTileSprite2.z);
    float sz = sin(aTileSprite2.z);

    modelview[0][0]  =  cz;
    modelview[0][1]  =  sz;

    modelview[1][0]  =  -sz;
    modelview[1][1]  = cz;
        
    return uMVPMatrix * modelview;
}